(cl:in-package :cl-user)

(require :cmp)

(ext:install-bytecodes-compiler)

#-quicklisp
(let ((quicklisp-init (merge-pathnames "quicklisp/setup.lisp"
                                       (user-homedir-pathname))))
  (when (probe-file quicklisp-init)
    (load quicklisp-init)))


(defparameter *ndk-path* nil)
(defparameter *work-path* nil)
(defparameter *lib-path* nil)
(defparameter *ecl-path* nil)
(defparameter *android-api-version* 23)
(defparameter *system* nil)
(defparameter *sources* nil)
(defparameter *build-dir* nil)


(defun merge-ecl-path (&optional path)
  (uiop:native-namestring (merge-pathnames (or path "") *ecl-path*)))


(defun merge-ecl-versioned-path (&optional path)
  (let* ((lib-path (merge-ecl-path "lib/"))
         (versioned-path (first (uiop:subdirectories lib-path))))
    (unless versioned-path
      (error "Failed to find versioned precompiled binaries"))
    (uiop:native-namestring (merge-pathnames (or path "") versioned-path))))


(defun merge-ndk-path (&optional path)
  (uiop:native-namestring (merge-pathnames (or path "") *ndk-path*)))


(defun merge-ndk-toolchain-path (&optional path)
  (merge-ndk-path (merge-pathnames path "toolchains/llvm/prebuilt/linux-x86_64/")))


(defun merge-work-path (&optional path)
  (uiop:native-namestring
   (merge-pathnames (or path "")
                    (uiop:pathname-directory-pathname (or *work-path*
                                                          *load-pathname*)))))


(defun merge-lib-path (&optional path)
  (uiop:native-namestring
   (merge-pathnames (or path "")
                    (uiop:pathname-directory-pathname (or *lib-path*
                                                          *load-pathname*)))))


(defun ecl-config (&rest flags)
  (uiop:run-program (list* (merge-ecl-path "bin/ecl-config") flags)
                    :output :string))


(defun string+ (&rest args)
  (format nil "窿狎珞┅ㄤ彐躅轭轸筱蜷痿ī箦翩驽狒躜弩扉篝乎铋横钿蝻殇横狎汨洞轰祜疱洪骀榄箅轲滹沲礤铘狒轱横扉孱黠螂蟓犷潋镩洵怩殪溴铙弭溟骀弪孱沐驽狒躜弩Ж壶付壶付洞壶付叨红轭貘乎铋轰狎鏖瑚轭瑚轭滹黧瑚轭巢瑚轭洞换沆遽铛骀礤翳镤轰祜疱恒惬轰骀椹┅祜镳骘钺礤鲠祯濠镱蹰镳恒镯磲钿扉铄狎珲礤铘螬怡＇沅潋滹ㄣ镱è篝蜷铉钺礤钿擘箦翩钿氕疳翳礤蜱瀛疳翳钺礤蹰镳哄铙躜瀛溟蝈泗矧疳翳钺礤鲠祯濠蹰镳虹弭泖洎┅è篝蜷铉钺礤邈膦箦翩邈飙疳翳礤蜱瀛疳翳钺礤蹰镳哄铙躜瀛溟蝈泗矧疳翳钺礤鲠祯濠蹰镳虹弭泖洎┅è篝蜷铉钺礤黠螂溟颌箦翩黠螂疳翳礤蜱瀛疳翳钺礤蹰镳哄铙躜瀛溟蝈泗矧疳翳钺礤鲠祯濠蹰镳虹弭泖洎┅è篝蜷铉钺礤扉猸溟颌箦翩扉猸疳翳礤蜱瀛疳翳钺礤蹰镳哄铙躜瀛溟蝈泗矧疳翳钺礤鲠祯濠蹰镳虹弭泖洎┅è篝蜷铉钺礤簌篝屙箦翩簌篝屙鲠祯濠è篝蜷铉钺礤怩殪洵溟颌箦翩怩殪洵溟颡礤蜱瀛疳翳钺礤蹰镳哄铙躜瀛溟蝈泗矧疳翳钺礤鲠祯濠蹰镳虹弭泖洎┅┅躅戾篌ㄡ钿钿氕疳翳邈飙疳翳簌篝屙ㄥ蝌矧⑼轶箝铉狎珲礤铘螽五邃钿氍邈飕簌篝屙┅痱镧ㄤ彐沆狍玑翳弪箫躜沐蟓镳ㄡ箐婧铒瞽痱镳徵狒轭绛镳弪狒轱瞟īㄤ彐礤翳镤狍滏猴疱蜥糸镱滹铄è玑翳弪箫躜沐蟓镳ㄣ狍滏后篝屙┅铋飑ㄤ彐礤翳镤狍滏吼弪骘蝽è玑翳弪箫躜沐蟓镳ㄣ狍滏后篝屙┅箦翩箫躜沐螵ㄡ箐婧轭瘐舡骈戾悌┅ㄤ彐礤翳镤狍滏洪铕豸骈戾è玑翳弪箫躜沐蟓镳ㄣ狍滏后篝屙┅麒孱铒ㄡ箐婧衡蹰祠轭簌篝屙悌祜镳骘篚轭ㄡ箐婧蝈聃轵邃泔眇镱孱趔虹镝飙镳弪狒轱п箐婧祜徜镳弘邋瓠镳弪狒轱п箐婧泔眇殪瀛镳猴翳弪簌篝屙舂麒孱豉疱篚п箐婧沆箫躜沐骈戾狃疱钿ㄡ箐婧轭瘐舡骈戾ㄡ箐婧磲脲镳弪狒轱п箐婧泔眇殪瀛镳篚猢┅┅ㄤ彐躅泔祆邈舡箫躜沐īㄡ箐婧镳弪狒х狒桢颦箫躜沐蟓镳簌篝屙┅ㄤ彐躅箦趱瓠泔眇殪弪īㄥ艉轭篝犰飙悱泔眇殪弪箦翩愫邯邈飙轭沆蹁瀛溟蝈泗矧礤蜱瀛邈飙疳翳㈤钽祯溴愫邯邈飙扉怛狎溟蝈泗矧礤蜱瀛邈飙疳翳㈧殁┅箦翩愫邯沣礤蜱瀛钿氕麸镬汨衢瞽疳翳ㄦ矧磲铋⑩轭後蜚瓒喘扉铛犷潋镩濑镰沆犷纰犷潋镩洵狃榄鲥蝮轱瞠┅愫邯熹礤蜱瀛钿氕麸镬汨衢瞽疳翳⑩轭後蜚瓒喘扉铛犷潋镩洵熹愫邯狎礤蜱瀛钿氕麸镬汨衢瞽疳翳⑩轭後蜚瓒喘扉铛犷潋镩洵狎愫邯蜥铎殁礤蜱瀛钿氕麸镬汨衢瞽疳翳⑩轭後蜚瓒喘扉铛犷潋镩洵蜥铎殁愫邯沣骒徵螵篝蜷铉ㄥ沆泔铈殓沔灬珞牧文蚁赡男塘云弦瓦廖囊仙喜嫘擅骖锃泔眄镱倪匀遗聊哂疗泞散礤蜱瀛邈飙疳翳┅愫邯熹骒徵螵篝蜷铉挞礤蜱瀛邈飙疳翳㈧殁戾沆熹祉扃祉沆狩邃戽殪犴孱舢沆狩邃祗潇伯沆狩邃挞礤蜱瀛钿氕麸镬汨衢瞽疳翳Ⅲ篁镲舣躞虔扉獐後蜚瓒喘扉铛犷潋镩浏挞礤蜱瀛扉猸疳翳⑨蝽洞龈岑┅愫邯熹蝠狒瑾铋愫邯熹箬狎邃骒徵螵篝蜷铉箬狎邃愫邯熹骒徵螵愫邯熹怩钿戾骒徵螵愫邯熹箬狎邃骒徵螵┅ㄤ彐磲泸鏖翳翦眇矧狎溟蝈泗矧è溟颟怙澌怙澌戾è繇瓠骈戾ㄧ孱簌愆┅啜蹰镳瑚轸璀翦眇矧狎骈戾ê疳翳钺礤繇瓠骈戾戾è溟蹰镳哄铙躜瀛溟蝈泗矧疳翳钺礤礤蜱瀛疳翳钺礤篝蜷铉ㄦ殪瀛钺礤篝蜷铉繇瓠骈戾溟虔蹰镳吼狒桀犴瀛溟蝈泗矧疳翳钺礤繇瓠骈戾┅┅ㄥ铙躜瀛溟蝈泗矧殄蟓屮轶溟颟躅鏖钿痱雉邈痱镧棱镤蹰镳轰屐弭瀛溟蝈泗矧趄邋溟忽犰殇狒洪姝滹弩铒舡屮轶洪珙矧濠┅┅ㄤ彐躅怩殪洵簌篝屙轭溟ㄢ蹰熹溟颟灬忮祗èャ镯痖戾箫躜沐戾舄è溴篝轭狒轱瞽箫躜沐礤蜱瀛疳翳钺礤蹰镳候屐狒轹辁瀛疳翳钺礤溟蝈泗矧箫躜沐怩殪洵溟颟ㄤ弩糸钺糸镱镡赍泗礤蜱瀛疳翳钺礤篝蜷铉ㄦ殪瀛钺礤篝蜷铉溴篝轭狒轱瞽箫躜沐铫溴篝轭狒轱瞽箫躜沐┅躅戾篌蹰镳烘殪瀛屮轶趔溴篝轭狒轱瞽镡赍泗ㄥ铙躜瀛溟蝈泗矧殄蟓屮轶溴篝轭狒轱瞽镡赍泗ㄦ矧磲τ秕蜚搴窿ο怅邈艉立箫躜沐溴篝轭狒轱瞽镡赍泗ㄣ镯痖戾骈戾箫躜沐猴豸瘐舡骈戾溴篝轭狒轱瞽镡赍泗后篝屙舂溴篝轭狒轱瞽镡赍泗┅ē泔眇殪瀛箫躜沐ī祜镳骘箫躜沐轭箫躜沐螵泔祆邈ē泔眇殪箫躜沐┅┅戾è镡赍泗啜ē泔眇殪礤蜱瀛黠螂疳翳⑨貘痱镬镧蹂扉箴┅礤蜱瀛邈飙鲥蝮轱铄洵疳翳㈧殁狍滏幄括ャ镯痖戾箫躜沐螬ē泔眇殪礤蜱瀛黠螂疳翳⑨貘屦殪镧蹂扉箴┅┅ㄣ衡蹰熹箬狎邃扉怛狎礤蜱瀛疳翳钺礤㈧殁犰殄铟矧塍箫蹰镳哄铙躜瀛溟蝈泗矧疳翳钺礤蹰镳虹弭泖洎┅红轶瓠骈戾镡赍泗洪铋舡钺礤⑦哚扉孱喵矧塍哚钿蝻殇唛铋簪┅┅ㄤ彐躅怩殪洵簌篝屙ī戾è驽狒躜弩扉篝恒惬铙弭溟骀弪孱沐驽狒躜弩Ж换沆遽铛骀礤翳镤轰祜疱恒惬轰骀椹┅┅ㄩ怩殪洵溟颡ㄢ蹰熹簌篝屙轭溟怩殪洵溟颡鏖翳翦眇矧狎溟蝈泗矧ㄢ蹰熹溟颟ㄢ蹰熹簌篝屙轭溟怩殪洵溟颟┅┅躅戾篌礤礅弪轰弼盹溴驽狒躜弩ㄩ铋舡筱蜷痿ㄣ镬戾泗箫躜沐螬换痱屐镝簌篝屙忮骘蝈泔眇殪狒轱ㄡ箐婧祜徜簌篝屙簌篝屙箦趱瓠泔眇殪弪ㄢ蹰熹簌篝屙┅